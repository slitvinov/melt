#!/bin/sh

awk -v me=bin -v n=20 '
END {
    print Keys
    for (b = 0; b < n; b++) {
        for (i = 1; i <= NF; i++)
            printf Data[i, b]/Cnt[b] " "
        printf "\n"
    }
}

FNR == 1 {
    flag = 0
}

flag {
    y = s("y")
    b = bin(y)
    Cnt[b] ++
    for (i = 1; i <= NF; i++)
        Data[i, b] += $i
}

/^ITEM: BOX BOUNDS/ {
    getline
    getline
    l = $2
    getline
}

/^ITEM: ATOMS/ {
    parse()
    flag = 1
}

function bin(x) {
    return int(x / l * n)
}

function parse(   k, a) {
    sub(/^ITEM: ATOMS /, "")
    Keys = $0
    split($0, a)
    for (k in a)
        _Keys[a[k]] = k
}

function s(k) {
    if (!(k in _Keys))
        err("unknown key: " k)
    k = _Keys[k]
    return $k
}

function err(s)
{
    print me ": " s | "cat >&2"
    exit(2)
}

' "$@"
